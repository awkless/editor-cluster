" SPDX-FileCopyrightText: 2023-2024 Jason Pena <jasonpena@awkless.com>
" SPDX-License-Identifier: MIT

"==============================================================================
" General Settings
"==============================================================================

" Tiny VIM does not have "eval".
if has("eval")
  let skip_defaults_vim = 1
endif

" Do not enforce Vi compatiblity.
set nocompatible

" Enable filetype plugins.
filetype plugin on
filetype indent on

" Auto read when a file is modified from the outside.
set autoread
au FocusGained,BufEnter * silent! checktime

" Set lines of history.
set history=1000

" Establish default file settings.
let $LANG = 'en'
set langmenu=en
set encoding=utf8
set ffs=unix,dos,mac

" Search settings.
set smartcase
set hlsearch
set incsearch
set lazyredraw

" Disable backup features.
set nobackup
set nowb
set noswapfile

" Stop complaints about switching buffer with changes.
set hidden

" Make backspace more natural.
set backspace=indent,eol,start

" Turn on persistent undo.
try
  set undodir=~/.vim_runtime/temp_dirs/undodir
  set undofile
catch
endtry

" Ignore compiled files
set wildignore=*.o,*~,*.pyc
if has("win16") || has("win32")
  set wildignore+=.git\*,.hg\*,.svn\*
else
  set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.DS_Store
endif

" Delete trailing white space on save.
if has("autocmd")
  autocmd BufWritePre *.txt,*.js,*.py,*.h,*.hpp,*.c,*.cpp,*.rs,*.toml,*.sh
    \ :call CleanExtraWhitespace()
endif

" Specify the behavior when switching between buffers
try
  set switchbuf=useopen,usetab,newtab
  set stal=2
catch
endtry

" Return to last edit position when opening files (You want this!)
au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$")
  \| exe "normal! g'\""
\| endif

"==============================================================================
" Plugins
"==============================================================================

" Install vim-plug if not found.
if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
endif

" Run PlugInstall if there are missing plugins.
autocmd VimEnter * if len(filter(values(g:plugs), '!isdirectory(v:val.dir)'))
  \| PlugInstall --sync | source $MYVIMRC
\| endif

if filereadable(expand("~/.vim/autoload/plug.vim"))
  call plug#begin('~/.local/share/vim/plugins')
  " General stuff.
  Plug 'tpope/vim-sleuth'
  Plug 'tpope/vim-commentary'

  " Git stuff.
  Plug 'tpope/vim-fugitive'
  Plug 'airblade/vim-gitgutter'

  " LSP stuff.
  Plug 'neoclide/coc.nvim', {'branch': 'release'}

  " UI stuff.
  Plug 'srcery-colors/srcery-vim'
  Plug 'rust-lang/rust.vim'
  Plug 'bfrg/vim-cpp-modern'
  call plug#end()
endif

"==============================================================================
" UI Options
"==============================================================================

" Line width limit.
set textwidth=100
set colorcolumn=80,100,120
set cursorline
set signcolumn=yes
set wrap
set nolist
set linebreak
set showbreak=$

" Configure command completion
set wildmenu
set wildmode=longest:full,full
set wildoptions=pum

" Configure colorscheme
syntax on
set t_Co=256
set termguicolors
set background=dark

try
  colorscheme srcery
catch
  colorscheme default
endtry

" Fold settings.
set foldmethod=indent
set nofoldenable

" Make splits more natural.
set splitbelow
set splitright

" Configure statusline.
set laststatus=2
set ruler
set updatetime=100
set cmdheight=1

" Highlight trailing whitespace.
match IncSearch '\s\+$'

" Bracket settings.
set mat=3

" Disable mouse.
set mouse=
set ttymouse=

" Faster scrolling.
set ttyfast

" Set cursor position.
set scrolloff=999

" Remove annoying error sounds.
set noerrorbells
set novisualbell
set t_vb=
set tm=500

" Highlight the symbol and its references when holding the curser.
if has("autocmd")
  autocmd CursorHold * silent call CocActionAsync('highlight')
endif

"==============================================================================
" Keymappings
"==============================================================================

" Make comma the defalt leader key.
let mapleader=","

" Use jj as a substitute for <esc>.
imap jj <esc>

" Remove with Windows ^M when encodings get messed up somehow.
noremap <leader>m mmHmt:%s/<c-v><cr>//ge<cr>'tzt'm

" Move to current directory of file...
nmap <leader>cd :cd %:p:h<cr>:pwd<cr>

" Toggle paste mode...
nmap <leader>pp :setlocal paste!<cr>

" Move through args.
map <leader>al :w<cr>:n<cr>
map <leader>ah :w<cr>:N<cr>

" Window movement.
map <C-j> <C-W>j
map <C-k> <C-W>k
map <C-h> <C-W>h
map <C-l> <C-W>l
map <C-e> <C-W>=

" Close the current buffer.
map <leader>bd :Bclose<cr>:tabclose<cr>gT

" Close all the buffers.
map <leader>ba :bufdo bd<cr>

" Move between buffers.
map <leader>l :bnext<cr>
map <leader>h :bprevious<cr>

" Useful mappings for managing tabs.
map <leader>tn :tabnew<cr>
map <leader>to :tabonly<cr>
map <leader>tc :tabclose<cr>
map <leader>tm :tabmove
map <leader>t<leader> :tabnext<cr>

" Let 'tl' toggle between this and the last accessed tab.
let g:lasttab = 1
nmap <leader>tl :exe "tabn ".g:lasttab<CR>
au TabLeave * let g:lasttab = tabpagenr()

" Opens a new tab with the current buffer's path.
map <leader>te :tabedit <C-r>=escape(expand("%:p:h"), " ")<cr>/

" Fast saving.
nmap <leader>w :w!<cr>

" Use W in command mode to sudo save a file.
command! W execute 'w !sudo tee % >/dev/null' <bar> edit!
nmap <leader>W :W<cr>

" Use tab to trigger completion with characters ahead.
inoremap <silent><expr> <tab>
  \ coc#pum#visible() ? coc#pum#next(1) :
  \ CheckBackspace() ? "\<tab>" :
  \ coc#refresh()
inoremap <expr><s-tab> coc#pum#visible() ? coc#pum#prev(1) : "\<c-h>"

" Make <cr> accept selected completion item or notify coc.nvim to format.
" Keymap <c-g>u breaks current undo.
inoremap <silent><expr> <cr> coc#pum#visible() ? coc#pum#confirm()
  \: "\<c-g>u\<cr>\<c-r>=coc#on_enter()\<cr>"

" Use <c-tab> to trigger completion.
inoremap <silent><expr> <c-tab> coc#refresh()

" Use K to show documentation in preview window.
nnoremap <silent> K :call ShowDocumentation()<cr>

" Use '[g' and ']g' to navigate diagnostics.
" Use ':CocDiagnostics' to get all diagnostics of current buffer.
nmap <silent><nowait> [g <Plug>(coc-diagnostic-prev)
nmap <silent><nowait> g] <Plug>(coc-diagnostic-next)

" Goto code navigation.
nmap <silent><nowait> gd <Plug>(coc-definition)
nmap <silent><nowait> gy <Plug>(coc-type-definition)
nmap <silent><nowait> gi <Plug>(coc-implementation)
nmap <silent><nowait> gr <Plug>(coc-references)

" Symbol renaming.
nmap <leader>rn <Plug>(coc-rename)

" Jump between hunks for git gutter...
nmap <leader>gh <Plug>GitGutterNextHunk
nmap <leader>gl <Plug>GitGutterPrevHunk
nmap <leader>ga <Plug>GitGutterStageHunk
nmap <leader>gu <Plug>GitGutterUndoHunk

" Spell checking...
nmap <leader>ss :setlocal spell!<cr>
nmap <leader>sl ]s
nmap <leader>sh [s
nmap <leader>sa zg
nmap <leader>sc z=

"==============================================================================
" Helpers
"==============================================================================

" Check previous character of backspace
function! CheckBackspace() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1] =~# '\s'
endfunction

" Show documentation on target symbol.
function! ShowDocumentation()
  if CocAction('hasProvider', 'hover')
    call CocActionAsync('doHover')
  else
    call feedkeys('K', 'in)
  endif
endfunction

" Clean extra whitespaces.
function! CleanExtraWhitespace()
  let save_cursor = getpos(".")
  let old_query = getreg('/')
  silent! %s/\s\+$//e
  call setpos('.', save_cursor)
  call setreg('/', old_query)
endfunction

" Don't close window, when deleting a buffer
command! Bclose call <SID>BufcloseCloseIt()
function! <SID>BufcloseCloseIt()
  let l:currentBufNum = bufnr("%")
  let l:alternateBufNum = bufnr("#")

  if buflisted(l:alternateBufNum)
    buffer #
  else
    bnext
  endif

  if bufnr("%") == l:currentBufNum
    new
  endif

  if buflisted(l:currentBufNum)
    execute("bdelete! ".l:currentBufNum)
  endif
endfunction
